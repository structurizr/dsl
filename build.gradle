apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

defaultTasks 'clean', 'compileJava', 'test'

repositories {
    mavenCentral()
}

dependencies {
    api 'com.structurizr:structurizr-client:1.14.1'
    implementation 'com.structurizr:structurizr-documentation:1.0.1'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1.3'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.3'

    testImplementation 'org.codehaus.groovy:groovy-jsr223:3.0.12'
    testImplementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.7.10'
    testImplementation 'org.jruby:jruby-core:9.3.6.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'Structurizr DSL'
group = 'com.structurizr'
version = '1.20.0'

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                'Implementation-Title':   description,
                'Implementation-Version': version
        )
    }
}

publishing {
    repositories {
        maven {
            name = "ossrh"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Structurizr DSL'
                description = 'Structurizr DSL'
                url = 'https://github.com/structurizr/dsl'

                scm {
                    connection = 'scm:git:git://github.com/structurizr/structurizr-dsl.git'
                    developerConnection = 'scm:git:git@github.com:structurizr/structurizr-dsl.git'
                    url = 'https://github.com/structurizr/dsl'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = "simon"
                        name = "Simon Brown"
                        email = "simon@structurizr.com"
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
